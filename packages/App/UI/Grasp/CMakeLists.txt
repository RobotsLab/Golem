###############################################################################
#
# Golem App/UI/Grasp application
#
###############################################################################

if(GOLEM_BUILD_APP_UI)
	option(GOLEM_BUILD_APP_GRASP_INTEROP "Build App/UI/Grasp Interop interface" YES)
	mark_as_advanced(GOLEM_BUILD_APP_GRASP_INTEROP)

	SET(PACKAGE_DIR "${PACKAGE_APP_UI}/Grasp")

	# OpenCV
	FIND_PACKAGE(OpenCV REQUIRED)

	FILE(GLOB_RECURSE PACKAGE_SOURCES "${PACKAGE_DIR}/src/*.cpp")
	FILE(GLOB_RECURSE PACKAGE_HEADERS "${PACKAGE_DIR}/include/*.h")
	FILE(GLOB_RECURSE PACKAGE_FILES "${PACKAGE_DIR}/resources/*.xml")
	FILE(GLOB_RECURSE PACKAGE_FILES_BIN "${PACKAGE_DIR}/resources/*.pcd" "${PACKAGE_DIR}/resources/*.wp" "${PACKAGE_DIR}/resources/*.cluster"  "${PACKAGE_DIR}/resources/*.model" "${PACKAGE_DIR}/resources/*.query")

	ADD_EXECUTABLE(GolemAppGrasp ${PACKAGE_SOURCES} ${PACKAGE_HEADERS} ${PACKAGE_FILES})

	if(WIN32)
		TARGET_COMPILE_OPTIONS(GolemAppGrasp PUBLIC /D_WINSOCK_DEPRECATED_NO_WARNINGS /wd4005)
	endif()

	if(GOLEM_BUILD_APP_GRASP_INTEROP)
		TARGET_COMPILE_DEFINITIONS(GolemAppGrasp PUBLIC _GOLEM_APP_GRASP_INTEROP_=1)
		TARGET_LINK_LIBRARIES(GolemAppGrasp GolemApp GolemContact GolemInteropGolem GolemInteropPCL)
	else(GOLEM_BUILD_APP_GRASP_INTEROP)
		TARGET_LINK_LIBRARIES(GolemAppGrasp GolemApp GolemContact)
	endif(GOLEM_BUILD_APP_GRASP_INTEROP)

	TARGET_INCLUDE_DIRECTORIES(GolemAppGrasp PUBLIC ${PACKAGE_DIR}/include ${OPENCV_INCLUDE_DIRS})

	COPY_FILES(GolemAppGrasp ${RUNTIME_OUTPUT_DIRECTORY} ${PACKAGE_FILES})
	COPY_FILES(GolemAppGrasp ${RUNTIME_OUTPUT_DIRECTORY} ${PACKAGE_FILES_BIN})
	SET_PROPERTY(TARGET GolemAppGrasp PROPERTY RELEASE_POSTFIX ${CMAKE_RELEASE_POSTFIX})
	SET_PROPERTY(TARGET GolemAppGrasp PROPERTY DEBUG_POSTFIX ${CMAKE_DEBUG_POSTFIX})
	
	if (CPACK_BUILD_CORE)
		INSTALL(TARGETS GolemAppGrasp RUNTIME DESTINATION bin COMPONENT app_execs)
		INSTALL(FILES ${PACKAGE_HEADERS} DESTINATION include/Golem/App/Grasp/ COMPONENT app_headers)
		INSTALL(FILES ${PACKAGE_FILES} DESTINATION bin COMPONENT app_configs)
		INSTALL(FILES ${PACKAGE_FILES_BIN} DESTINATION bin COMPONENT app_configs)
	endif (CPACK_BUILD_CORE)

	SET_PROPERTY(TARGET GolemAppGrasp PROPERTY PROJECT_LABEL "Grasp")
	SET_PROPERTY(TARGET GolemAppGrasp PROPERTY FOLDER "App/UI")
	SOURCE_GROUP("Include Files" FILES ${PACKAGE_HEADERS})
	SOURCE_GROUP("Resource Files" FILES ${PACKAGE_FILES})
endif(GOLEM_BUILD_APP_UI)
