###############################################################################
#
# Golem Plugin/Sensor/CameraOpenNI library
#
###############################################################################

option(GOLEM_BUILD_PLUGIN_SENSOR_CAMERAOPENNI "Build Golem Plugin/Sensor OpenNI plugin" NO)
mark_as_advanced(GOLEM_BUILD_PLUGIN_SENSOR_CAMERAOPENNI)

if(GOLEM_BUILD_PLUGIN_SENSOR AND GOLEM_BUILD_PLUGIN_SENSOR_CAMERAOPENNI)
	SET(PACKAGE_DIR "${PACKAGE_PLUGIN_SENSOR}/CameraOpenNI")

	SET(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${GOLEM_PROJECT_CMAKE_MODULES}")
	
	# OpenNI2
	FIND_PACKAGE(OpenNI2 REQUIRED)
	# OpenCV
	FIND_PACKAGE(OpenCV REQUIRED)

	FILE(GLOB_RECURSE PACKAGE_SOURCES "${PACKAGE_DIR}/src/*.cpp")
	FILE(GLOB_RECURSE PACKAGE_HEADERS "${PACKAGE_DIR}/include/*.h")
	FILE(GLOB_RECURSE PACKAGE_FILES "${PACKAGE_DIR}/resources/*.xml")
	FILE(GLOB_RECURSE PACKAGE_FILES_CAL "${PACKAGE_DIR}/resources/*.cal")

	ADD_LIBRARY(GolemCameraOpenNI SHARED ${PACKAGE_SOURCES} ${PACKAGE_HEADERS} ${PACKAGE_FILES} ${PACKAGE_FILES_CAL})
	TARGET_COMPILE_OPTIONS(GolemCameraOpenNI PUBLIC ${CMAKE_DLL_EXPORT_FLAGS})

	TARGET_LINK_LIBRARIES(GolemCameraOpenNI GolemTools ${OPENNI2_LIBRARIES})
	
	TARGET_COMPILE_DEFINITIONS(GolemCameraOpenNI PUBLIC ${OPENNI2_DEFINITIONS})
	TARGET_INCLUDE_DIRECTORIES(GolemCameraOpenNI PUBLIC ${PACKAGE_DIR}/include ${OPENNI2_INCLUDE_DIRS} ${OPENCV_INCLUDE_DIRS})

	COPY_FILES(GolemCameraOpenNI ${RUNTIME_OUTPUT_DIRECTORY} ${PACKAGE_FILES})

	if (CPACK_BUILD_CORE)
		if (WIN32)
			INSTALL(TARGETS GolemCameraOpenNI RUNTIME DESTINATION bin COMPONENT sensor_execs)
		elseif (UNIX)
			INSTALL(TARGETS GolemCameraOpenNI LIBRARY DESTINATION bin COMPONENT sensor_execs)
		endif()
		INSTALL(FILES ${PACKAGE_HEADERS} DESTINATION include/Golem/Sensor/CameraOpenNI/ COMPONENT sensor_headers)
		INSTALL(FILES ${PACKAGE_FILES} DESTINATION bin COMPONENT sensor_configs)
	endif (CPACK_BUILD_CORE)

	SET_PROPERTY(TARGET GolemCameraOpenNI PROPERTY PROJECT_LABEL "CameraOpenNI")
	SET_PROPERTY(TARGET GolemCameraOpenNI PROPERTY FOLDER "Plugin/Sensor")
	SOURCE_GROUP("Include Files" FILES ${PACKAGE_HEADERS})
	SOURCE_GROUP("Resource Files" FILES ${PACKAGE_FILES} ${PACKAGE_FILES_CAL})
endif(GOLEM_BUILD_PLUGIN_SENSOR AND GOLEM_BUILD_PLUGIN_SENSOR_CAMERAOPENNI)
